// This Gradle script generates Javadocs after `delomboking` the code. This allows for javadocs to include
// description for getters and setters generated by lombok.
apply plugin: 'java'

configurations {
    lombok
}

dependencies { 
    lombok 'org.projectlombok:lombok:1.16.10'
}

sourceSets.each{ sourceSet -> 
    sourceSet.compileClasspath += configurations.lombok
    sourceSet.ext.delombok = new File(buildDir, "generated-src/delombok/" + sourceSet.name);
}

// Delombok the code
task delombok() {
    dependsOn configurations.compile
    dependsOn configurations.lombok
} << {
    File jarFile = null;
    configurations.lombok.resolvedConfiguration.resolvedArtifacts.find {
        if ("lombok".equals(it.name)) {
            jarFile = it.file;
        }
    }
    // Creating a command similar to what is provided in lombok website
    sourceSets.each{ sourceSet ->
        def classPath = sourceSet.compileClasspath.files.join(";")
        def delombokPath = sourceSet.ext.delombok
        delombokPath.mkdirs();
        javaexec {
            main = "-jar"
            args jarFile, "delombok"
            if (!classPath.empty) {
                args "-c", classPath
            }
            args "-d", delombokPath
            args sourceSet.allJava.srcDirs
        }
    }
}

// Override Javadoc to delombok first
javadoc {
    dependsOn delombok
    source = sourceSets.main.ext.delombok
}
